/****************************************************************************

Copyright (c) 2006 - 2012 Novell, Inc.
All Rights Reserved.

This program is free software; you can redistribute it and/or
modify it under the terms of version 2 of the GNU General Public License as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, contact Novell, Inc.

To contact Novell about this file by physical or electronic mail,
you may find current contact information at www.novell.com

****************************************************************************/
/**
 * File:        OUCDialogs.ycp
 * Module:      online-update-configuration
 * Summary:     Configure Online Update
 * Authors:     J. Daniel Schmidt <jdsn@suse.de>
 *
 * Configure Online Update Configuration Dialogs
 *
 * $Id: OUCDialogs.ycp 1 2008-09-10 13:20:02Z jdsn $
 */

{

//textdomain "registration";
textdomain "online-update-configuration";

import "OnlineUpdateConfiguration";

// module title
string moduleTitle = _("Online Update Configuration");
// translators: (default), meaning: "Current Update Repository: http://xyz/  (default)"
string defaultMark  = _("(default)");
// translators: (none), meaning "Current Update Repository: (none)"
string noRepo = _("(none)");

// frame title
string updateRepository = _("Update Repository");
// frame title
string automaticOnlineUpdate = _("Automatic Online Update");

// translators: "Set Default" meaning:  Set the Update Repository to the default one
string setDefaultButtonLabel = _("Set Default");
// translators: a short button label called "Advanced"
string advancedMenuButtonLabel = _("Advanced");

// for category filter
string filterByCategory = _("Filter by Category");
// for category filter //translators: means: categories of patches
string patchCategories = _("Patch Categories");

string enabledMsg  = _("enabled");
string disabledMsg = _("disabled");

string editSoftwareRepositories = _("Edit Software Repositories");
string registerForSupport       = _("Register for support and get update repository");
string sendDataToSmolt          = _("Send hardware information to the smolt project");
string interval                 = _("Interval");
string skipInteractivePatches   = _("Skip Interactive Patches");
string autoAgreeWithLicenses    = _("Agree with Licenses");
string includeRecommends        = _("Include Recommended Packages");
string currentUpdateRepo        = _("Current Update Repository:");
string needToRegister           = _("In order to add the default update repository\nyou have to register this product.");
string runRegistrationNow       = _("Do you want to perform the registration now?");

string help_title = sformat( "<p><b>%1</b></p>", moduleTitle);
string help_para1 = sformat(_("<p>In <b>%1</b> the current update repository is shown.</p>"), updateRepository );
string help_para2 = sformat(_("<p>Press <b>%1</b> to use the default update repository.</p>"), setDefaultButtonLabel );
string help_para3 = sformat(_("<p>Find related actions in the <b>%1</b> menu.</p>"), advancedMenuButtonLabel );
string help_para4 = sformat(_("<p>In <b>%1</b> set up the automatic online update.</p>"), automaticOnlineUpdate);
string help_para5 = sformat(_("<p>Select an update interval and specify if interactive patches should be ignored and if licenses should be automatically agreed with.</p>"));
string help_para6 = sformat(_("<p>All packages that are recommended by an updated package will be installed when <b>%1</b> is enabled.</p>"), includeRecommends);
string help_para7 = sformat(_("<p>Category filter for patches can be configured in the section <b>%1</b>. Only patches of the listed categories will be installed. Others will be skipped.</p>"), patchCategories);


string getOUCHelp(symbol type)
{
    if (type == `autoyast)
    {
        return help_title + help_para4 + help_para5 + help_para6 + help_para7;
    }
    else
    {
        // disabled as the setting of the default update repo is not possible
        //return help_title + help_para1 + help_para2 + help_para3 + help_para4 + help_para5 + help_para6 + help_para7;
        return help_title + help_para4 + help_para5 + help_para6 + help_para7;
    }
    return "";
}




term getOUCDialog(symbol type)
{

    list<term> expertMenu = [ `item(`id(`repoManager), editSoftwareRepositories )
                            //  , `item(`id(`register),    registerForSupport )
                            //  , `item(`id(`smolt),       sendDataToSmolt )
                        ];

    list<term> updateIntervals = maplist(symbol intid, string intname, OnlineUpdateConfiguration::Intervals, {
        return `item( `id(intid), intname);
    });

    term upRepo = `Frame(updateRepository,
                        `HBox(
                            `HStretch(),
                            `VBox(
                                `Left( `Label( currentUpdateRepo )),
                                `Left( `Label(`id(`currentRepoURL), "" )),
                                `HBox(
                                    `PushButton(`id(`restoreDefault), `opt(`disabled), setDefaultButtonLabel),
                                    `MenuButton( advancedMenuButtonLabel, expertMenu)
                                )
                            ),
                            `HStretch()
                        )
                    );

    list<string> sortedCatKeys = [];
    sortedCatKeys = maplist(string s, term t, OnlineUpdateConfiguration::defaultCategories, { return s; });
    sortedCatKeys = sort(string a, string b, toset(sortedCatKeys), ``(a > b));
    list<term> allCategories = maplist(string s, sortedCatKeys, { return OnlineUpdateConfiguration::defaultCategories[s]:`item(); });

    term autoOnlineUp = `HVSquash(
      `VBox(
        `CheckBoxFrame(`id(`automaticOnlineUpdate), automaticOnlineUpdate, false,
          `HBox(
            `HSpacing(2),
            `VBox(
              `Left ( `ComboBox(`id(`updateInterval), interval, updateIntervals )),
              `VSpacing(0.2),
              `Left( `CheckBox(`id(`skipInteractivePatches), skipInteractivePatches, OnlineUpdateConfiguration::skipInteractivePatches == true ? true:false ) ),
              `VSpacing(0.2),
              `Left( `CheckBox(`id(`autoAgreeWithLicenses),  autoAgreeWithLicenses,  OnlineUpdateConfiguration::autoAgreeWithLicenses  == true ? true:false ) ),
              `VSpacing(0.2),
              `Left( `CheckBox(`id(`includeRecommends),  includeRecommends,  OnlineUpdateConfiguration::includeRecommends == true ? true:false ) ),
              `VSpacing(0.8),
              `CheckBoxFrame( `id(`category), filterByCategory,  false,
                `HBox(
                  `HSpacing(2),
                  `VBox(
                    `SelectionBox( `id(`categories), patchCategories, [] ),
                    `HBox(
                      `MinWidth(15, `ComboBox( `id(`catcustom), `opt(`editable), "", allCategories ) ),
                      `PushButton( `id(`catadd), Label::AddButton() ),
                      `HSpacing(4),
                      `PushButton( `id(`catdel), Label::DeleteButton() )
                    )
                  )
                )
              ),
              `VSpacing(0.8)
            ),
            `HSpacing(2)
          )
        ),
       `Right( `MenuButton( advancedMenuButtonLabel, expertMenu) ),
       `VStretch()
      )
    );


    term contents = nil;

    if (type == `autoyast)
    {
        contents = `VBox(  `VSpacing(1.5),  autoOnlineUp,  `VStretch(), `VSpacing(1.5) );
    }
    else
    {
        //contents = `HVSquash( `VBox(
        //             `VSpacing(1.5),  upRepo,
        //             `VSpacing(1.5),  autoOnlineUp, `VStretch(),  `VSpacing(1.5)
        //            ));

        // do not show the update repo restore section
        // repos do not identify themselves yet as update repos, and products do not define their update repo ID
        // can be reactivated when bnc#449842 is fixed and fully supported
        contents = `VBox(  `VSpacing(1.5),  autoOnlineUp,  `VStretch(), `VSpacing(1.5) );
    }

    return contents;

}


integer refreshCategoryList(string selected)
{
    OnlineUpdateConfiguration::currentCategories = toset(OnlineUpdateConfiguration::currentCategories);
    OnlineUpdateConfiguration::currentCategories = sort(string a, string b, OnlineUpdateConfiguration::currentCategories, ``(a < b));
    list<term> newcat = maplist(string onecat, OnlineUpdateConfiguration::currentCategories,  {
        onecat=filterchars(onecat, "abcdefghijklmnopqrstuvwxyz0123456789-_.");
        return OnlineUpdateConfiguration::defaultCategories[onecat]:(`item( `id(onecat), onecat));
    });
    UI::ChangeWidget( `id(`categories), `Items, newcat);

    if ( selected != nil && selected != "" )
    {
        UI::ChangeWidget( `id(`categories), `CurrentItem, selected);
    }
}


}
